allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

val newBuildDir: Directory =
    rootProject.layout.buildDirectory
        .dir("../../build")
        .get()
rootProject.layout.buildDirectory.value(newBuildDir)

subprojects {
    val newSubprojectBuildDir: Directory = newBuildDir.dir(project.name)
    project.layout.buildDirectory.value(newSubprojectBuildDir)
}
subprojects {
    project.evaluationDependsOn(":app")
}
subprojects {
    if (!project.state.executed) {
        afterEvaluate {
            if (project.hasProperty("android")) {
                val android = project.extensions.findByName("android")
                if (android is com.android.build.gradle.LibraryExtension) {
                    if (android.namespace.isNullOrEmpty()) {
                        val manifest = file("${project.projectDir}/src/main/AndroidManifest.xml")
                        if (manifest.exists()) {
                            val pkg = javax.xml.parsers.DocumentBuilderFactory.newInstance()
                                .newDocumentBuilder()
                                .parse(manifest)
                                .documentElement
                                .getAttribute("package")
                            if (pkg.isNotEmpty()) {
                                android.namespace = pkg
                            }
                        }
                    }
                    // Force compileSdk for old plugins compiled against outdated API levels
                    if (android.compileSdk != null && android.compileSdk!! < 36) {
                        android.compileSdk = 36
                    }
                }
            }
        }
    }
}

tasks.register<Delete>("clean") {
    delete(rootProject.layout.buildDirectory)
}
